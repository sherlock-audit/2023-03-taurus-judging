Ruhum

high

# User can screw reward distribution by depositing large amounts of collateral before reward distribution

## Summary
An attack can pay large amounts of debt by increasing their collateral amount right before rewards are distributed.

## Vulnerability Detail
The reward distribution can be gamed by an attacker. Rewards are distributed to users according to the amount of collateral they have deposited *at the time of the distribution*. By frontrunning the `TauDripFeed.distributeTauRewards()` function, the user can inflate their collateral amount right before rewards are distributed and then remove them after that. It allows them to pay off their debt without having to lock up any extra collateral.

Given that:
- Alice has 1000 debt and 2000 collateral
- Bob has 1000 debt and 2000 collateral
- 4 reward tokens are distributed

**1. TAU is distributed through the TauDripFeed contract (generally through the SwapHandler)**
```sol
    function _disburseTau() internal {
        if (tauWithheld > 0) {
            uint256 _currentCollateral = IERC20(collateralToken).balanceOf(address(this));

            if (_currentCollateral > 0) {
                // Get tokens to disburse since last disbursal
                uint256 _timeElapsed = block.timestamp - tokensLastDisbursedTimestamp;

                uint256 _tokensToDisburse;
                if (_timeElapsed >= DRIP_DURATION) {
                    _tokensToDisburse = tauWithheld;
                    tauWithheld = 0;
                } else {
                    _tokensToDisburse = (_timeElapsed * tauWithheld) / DRIP_DURATION;
                    tauWithheld -= _tokensToDisburse;
                }

                // Divide by current collateral to get the additional tokensPerCollateral which we'll be adding to the cumulative sum
                uint256 _extraRewardPerCollateral = (_tokensToDisburse * Constants.PRECISION) / _currentCollateral;

                cumulativeTauRewardPerCollateral += _extraRewardPerCollateral;

                tokensLastDisbursedTimestamp = block.timestamp;
            }
        }
    }
```
It calculates the amount of rewards to distribute per collateral token. **This does not distribute any rewards to users yet. None of their debt positions is modified**. $4 / 4000 = 0.001$ reward tokens are supposed to be distributed per collateral. Meaning, Alice's debt should be reduced by $2000 * 0.001 = 2$

**2. Before the rewards are distributed, Alice deposits a large amount of tokens**
Alice frontruns the transaction and deposits 1,000,000 tokens. That changes the math:
- Alice has 1000 debt and 1,002,000 collateral
- Bob has 1000 debt and 2000 collateral
$4 / 1,004,000 = 0.00000398406$ reward tokens are distributed per collateral.

Right after the Tau rewards are disbursed, Alice calls `modifyPosition()` to remove 1,000,000 collateral tokens to set her position back to its original state. In that call, the `updateReward()` modifier is called. For every collateral token she has her debt is paid off:
```sol
    modifier updateReward(address _account) {
        // Disburse available yield from the drip feed
        _disburseTau();

        // If user has collateral, pay down their debt and recycle surplus rewards back into the tauDripFeed.
        uint256 _userCollateral = userDetails[_account].collateral;
        if (_userCollateral > 0) {
            // Get diff between global rewardPerCollateral and user lastUpdatedRewardPerCollateral
            uint256 _rewardDiff = cumulativeTauRewardPerCollateral -
                userDetails[_account].lastUpdatedRewardPerCollateral;

            // Calculate user's TAU earned since the last update, use it to pay off debt
            uint256 _tauEarned = (_rewardDiff * _userCollateral) / Constants.PRECISION;

            if (_tauEarned > 0) {
                uint256 _userDebt = userDetails[_account].debt;
                if (_tauEarned > _userDebt) {
                    // If user has earned more than enough TAU to pay off their debt, pay off debt and add surplus to drip feed
                    userDetails[_account].debt = 0;

                    _withholdTau(_tauEarned - _userDebt);
                    _tauEarned = _userDebt;
                } else {
                    // Pay off as much debt as possible
                    userDetails[_account].debt = _userDebt - _tauEarned;
                }

                emit TauEarned(_account, _tauEarned);
            }
        } else {
            // If this is a new user, add them to the userAddresses array to keep track of them.
            if (userDetails[_account].startTimestamp == 0) {
                userAddresses.push(_account);
                userDetails[_account].startTimestamp = block.timestamp;
            }
        }

        // Update user lastUpdatedRewardPerCollateral
        userDetails[_account].lastUpdatedRewardPerCollateral = cumulativeTauRewardPerCollateral;
        _;
    }
```
$0.00000398406 * 1,002,000 = 3.99203187251$ reward tokens are distributed to her. 

While both Alice and Bob had the same amount of collateral deposited that earned fees, Alice was able to steal the majority of it by frontrunning the reward distribution function to inflate her collateral amount. Since she's able to remove the collateral right after she inquires no loss besides the gas costs of the transaction.

To sum up:
1. Alice sets up a bot to watch for the transaction that distributes TAU rewards
2. Alice's bot frontruns the transaction and deposits a large amount of collateral to her position
3. The reward tx is executed
4. Alice calls `modifyPosition()` to remove her collateral. That claims Alice's rewards with her *current* collateral amount.

## Impact
User is able to steal other people's rewards allowing them to pay off their debt faster.

## Code Snippet
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/TauDripFeed.sol#L73-L98
## Tool used

Manual Review

## Recommendation
