jonatascm

high

# Computing collateral ratio does not take into account the tokens decimals

## Summary

The function `_computeCR()` is calculating incorrectly the collateral ratio when collateral and debt have different decimals

## Vulnerability Detail

Calculating the collateral ratio does not consider the collateral and debt decimals, this affects all functions that use `_computeCR()` function.

## POC

In the case of collateral is UDSC (decimals 6) and debit is TAU (decimals 8):

If Alice wants to borrow 1000 TAU for the price of $1 (price decimals 18), adding 1200 USDC as collateral, in this case, the ratio would be 120% but calculating through the function is:

```makefile
newCollRatio = (_coll * _price * Constants.PRECISION) / (_debt * 10 ** priceDecimals);

newCollRatio = (1200e6 * 1e18 * PRECISION) / (1000e18 * 1e18)
newCollRatio = (1.2 * e(-12)) * PRECISION
newCollRation = 0.000...00012%
```

There is a good difference is the decimals are different

Here is a POC made with foundry:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.15;

import "forge-std/Test.sol";
import "../contracts/Vault/BaseVault.sol";
import "../contracts/Controller/Controller.sol";
import "../contracts/Mocks/MockERC20.sol";
import "../contracts/Vault/YieldAdapters/GMX/IRewardRouter.sol";
import "../contracts/Oracle/CustomPriceOracle/CustomPriceOracle.sol";
import "../contracts/Oracle/Wrapper/CustomOracleWrapper.sol";
import "../contracts/Oracle/PriceOracleManager.sol";
import { Constants } from "../contracts/Libs/Constants.sol";
import { ERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";



//Custom VaultAdapter 
contract VaultAdapter is BaseVault {
    // Reward router for GMX. Note that this must be V1, not V2, for now.
    address public rewardRouter;

    constructor(
        address _controller,
        address _tau,
        address _collateralToken,
        address _rewardRouter
    ) {
        __BaseVault_init(_controller, _tau, _collateralToken);
        rewardRouter = _rewardRouter;
    }

    function collectYield() external {
        IRewardRouter(rewardRouter).claimFees(); // Claim WETH yield from staked esGmx and Glp
        IRewardRouter(rewardRouter).compound(); // Claim and stake esGmx and bnGmx earned from staked esGmx and Glp
    }

    function updateRewardRouter(address _newRewardRouter) external onlyMultisig {
        rewardRouter = _newRewardRouter;
    }

    function calcLiquidation(
        uint256 _accountCollateral,
        uint256 _accountDebt,
        uint256 _debtToLiquidate
    ) public view returns (uint256 collateralToLiquidate, uint256 liquidationSurcharge) {
        return _calcLiquidation(
            _accountCollateral,
            _accountDebt,
            _debtToLiquidate
        );
    }
}

//Custom ERC20 with 6 decimals
contract CustomToken is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        _mint(msg.sender, 500 ether);
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function decimals() public pure override returns (uint8) {
        return 6;
    }
}

contract VaulTest is Test{
    VaultAdapter vault;
    Controller _controller;
    MockERC20 _tau;
    MockERC20 _tgt;
    CustomToken _collateralToken;
    PriceOracleManager oracleManager;
    CustomPriceOracle customOracle;
    CustomOracleWrapper oracleWrapper;
    address _rewardRouter;


   function setUp() public {
        //Setting up tokens, oracle, controller and vault
        _tau = new MockERC20("Mock TAU", "TAU");
        _tgt = new MockERC20("Mock TGT", "TGT");

        _collateralToken = new CustomToken("Mock CustomToken", "CTK");

        //ARBITRUM_GMX_REWARD_ROUTER_V2_ADDRESS    
        _rewardRouter = address(0xB95DB5B167D75e6d04227CfFFA61069348d271F5);

        //CUSTOM ORACLE
        customOracle = new CustomPriceOracle("CUSTOM ORACLE", address(_collateralToken), 18);
        customOracle.registerTrustedNode(address(this));
        
        //ORACLE WRAPPER
        oracleWrapper = new CustomOracleWrapper();
        oracleWrapper.addOracle(address(_collateralToken), address(customOracle));

        //ORACLE MANAGER
        oracleManager = new PriceOracleManager();
        oracleManager.setWrapper(address(_collateralToken), address(oracleWrapper));

        //CONTROLLER
        _controller = new Controller(address(_tau), address(_tgt), address(this), address(this));
        _controller.setAddress(Constants.PRICE_ORACLE_MANAGER, address(oracleManager));
        
        //VAULT
        vault = new VaultAdapter(
            address(_controller),
            address(_tau),
            address(_collateralToken),
            address(_rewardRouter)
        );

    }


    function testWrongDecimals() public {
        customOracle.updatePrice(1e18); //1:1
        uint256 accountCollateral = 121 * 1e6; // 121 CTK
        uint256 accountDebt = 100 * 1e18; // 100 TAU
        uint256 debtToLiquidate = 100 * 1e18;
        
        
        _collateralToken.increaseAllowance(address(vault), accountCollateral);

        //@audit Even if the price is 1:1
        // the collateral ratio 121%
        // It will revert
        vm.expectRevert();
        //Create Position with 120CTK collateral and 100TAU debt
        vault.modifyPosition(
            accountCollateral,
            accountDebt,
            true,
            true
        );
    }
}

```

## Impact

The users could be liquidated even if they have the correct amount of collateral

## Code Snippet

[TauMath.sol#L11-27](https://github.com/sherlock-audit/2023-03-taurus-jonatascm/tree/main/taurus-contracts/contracts/Libs/TauMath.sol#L11-27)

```solidity
function _computeCR(
  uint256 _coll,
  uint256 _debt,
  uint256 _price,
  uint8 priceDecimals
) internal pure returns (uint256) {
  if (_debt > 0) {
    uint256 newCollRatio = (_coll * _price * Constants.PRECISION) / (_debt * 10 ** priceDecimals);

    return newCollRatio;
  }
  // Return the maximal value for uint256 if the account has a debt of 0. Represents "infinite" CR.
  else {
    // if (_debt == 0)
    return type(uint256).max;
  }
}
```

## Tool used

Manual Review

## Recommendation

It's recommended to use the collateral decimals and debt decimals in the account:

```diff
function _computeCR(
  uint256 _coll,
+	uint256 _collDecimals,
  uint256 _debt,
+	uint256 _debtDecimals,
  uint256 _price,
  uint8 priceDecimals
) internal pure returns (uint256) {
  if (_debt > 0) {
-    uint256 newCollRatio = (_coll * _price * Constants.PRECISION) / (_debt * 10 ** priceDecimals);
+   uint256 newCollRatio = (_coll * _debtDecimals * _price * Constants.PRECISION) / (_debt * _collDecimals * 10 ** priceDecimals);
   ... 
}
```