RaymondFam

medium

# The last output element of `getUsers()` and `getUsersDetailsInRange()` is omitted

## Summary
In LiquidationBot.sol, `fetchUnhealthyAccounts(_)` makes an external call to [`vault.getUsers()`](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/LiquidationBot/LiquidationBot.sol#L69) amidst its function logic to output a list of unhealthy accounts that is filtered from [`_startIndex`, `_startIndex + offset`]  inclusive. However, `getUsers()` has a logic flaw that is only returning all requested user addresses except the last one, apparently entailing an issue that is beyond a simple code and comment mismatch.

## Vulnerability Detail
As can be seen from the code block below, the array size of `users` is clearly declared as start and end index inclusive, i.e. `_end - _start + 1`. However, the `for` loop that ensues does not have the last element included because of the inappropriate use of `<` in the iterative conditions.

```solidity
    /** @dev Get the user addresses in the range given start and end index
     * note the start and end index are inclusive
     */
    function getUsers(uint256 _start, uint256 _end) public view returns (address[] memory users) {
        if (_end > getUsersCount() || _start > _end) revert indexOutOfBound();

        users = new address[](_end - _start + 1);

        for (uint256 i = _start; i < _end; ++i) {
            users[i - _start] = userAddresses[i];
        }
    }
```
## Impact
Consequently, the front-end bot making a read call to `fetchUnhealthyAccounts()` is going to receive an output return that may have one unhealthy account short if the omitted element from `getUsers()` happens to be an unhealthy one.

The impact is inversely proportional to the amount of requested user addresses, and could be associated with a miss out probability rate of 0.5 (50%) if the request entailed was as little as 2 elements, and a total miss, i.e. 1.0 (100%) if [`offset == 0`](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/LiquidationBot/LiquidationBot.sol#L69).

## Code Snippet
[File: LiquidationBot.sol#L69](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/LiquidationBot/LiquidationBot.sol#L69)
[File: BaseVault.sol#L170-L181](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L170-L181)

Additionally, the logic flaw has also been associated with `getUsersDetailsInRange()` which is currently not invoked by any other function in the files under audit scope.

[File: BaseVault.sol#L157-L168](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L157-L168)

```solidity
    /** @dev Get the user details in the range given start and end index.
     * note the start and end index are inclusive
     */
    function getUsersDetailsInRange(uint256 _start, uint256 _end) public view returns (UserDetails[] memory users) {
        if (_end > getUsersCount() || _start > _end) revert indexOutOfBound();

        users = new UserDetails[](_end - _start + 1);

        for (uint i = _start; i < _end; ++i) {
            users[i - _start] = userDetails[userAddresses[i]];
        }
    }
```
## Tool used

Manual Review

## Recommendation
Consider refactoring the affected `getUsersDetailsInRange()` and `getUsers()` and as follows:

```diff
    function getUsersDetailsInRange(uint256 _start, uint256 _end) public view returns (UserDetails[] memory users) {
        if (_end > getUsersCount() || _start > _end) revert indexOutOfBound();

        users = new UserDetails[](_end - _start + 1);

-        for (uint i = _start; i < _end; ++i) {
+        for (uint i = _start; i <= _end; ++i) {
            users[i - _start] = userDetails[userAddresses[i]];
        }
    }

    function getUsers(uint256 _start, uint256 _end) public view returns (address[] memory users) {
        if (_end > getUsersCount() || _start > _end) revert indexOutOfBound();


        users = new address[](_end - _start + 1);

-        for (uint256 i = _start; i < _end; ++i) {
+        for (uint256 i = _start; i <= _end; ++i) {
            users[i - _start] = userAddresses[i];
        }
    }
```
