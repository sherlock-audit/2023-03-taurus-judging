Bauer

high

# Sandwich attack to reduce debt

## Summary

Sandwich attack to reduce debt

## Vulnerability Detail
Every time user modifys collateral and debt in any way , the protocol will update user's reward per collateral and pay off some of their debt. The modifier is executed before any function that modifies a user's collateral or debt. It first disburses TAU to the contract by updating cumulativeTauRewardPerCollateral, then calculate user's TAU earned since the last update, use it to pay off debt. The cumulativeTauRewardPerCollateral is calculated according to the following code. The larger the value ```tauWithheld``, the more for the reward per collateral.
```solidity
uint256 _currentCollateral = IERC20(collateralToken).balanceOf(address(this));

            if (_currentCollateral > 0) {
                // Get tokens to disburse since last disbursal
                uint256 _timeElapsed = block.timestamp - tokensLastDisbursedTimestamp;

                uint256 _tokensToDisburse;
                if (_timeElapsed >= DRIP_DURATION) {
                    _tokensToDisburse = tauWithheld;
                    tauWithheld = 0;
                } else {
                    _tokensToDisburse = (_timeElapsed * tauWithheld) / DRIP_DURATION;
                    tauWithheld -= _tokensToDisburse;
                }

                // Divide by current collateral to get the additional tokensPerCollateral which we'll be adding to the cumulative sum
                uint256 _extraRewardPerCollateral = (_tokensToDisburse * Constants.PRECISION) / _currentCollateral;

                cumulativeTauRewardPerCollateral += _extraRewardPerCollateral;
```
If the diff between global rewardPerCollateral and user lastUpdatedRewardPerCollateral is larger, the more user will be rewarded. We noticed that sometime the Keeper will swap the yield Token to Tau and increase the ```tauWithheld```. Here sandwich attack is possible.
1.Assume there are 1,000,000 collateral tokens in the vault and Bob has 100,000 , the ```tauWithheld``` is 80,000

2.Assume a keeper disburse 100,000 TAU to the contract.

3.Bob front-runs Vault.swapForTau or Vault.distributeTauRewards  calls the modifyPosition() to increase 100,000 collateral tokens into the vault. As a result, Bob has 200,000 collateral tokens and there are 1,200,000 collateral tokens in the vault. the ```tauWithheld``` is 80,000+x

4.Keeper disburse 100000 TAU to the contract, the ```tauWithheld``` is 80,000+100,000+x.

5.After 10s , Bob calls the ```modifyPosition()``` function to withdraw 100,000 collateral. His debut will reduced by 3,47
_tokensToDisburse =10 * 180,000/86400 
_extraRewardPerCollateral = _tokensToDisburse/1,200,000
_tauEarned = 3.4722222222222223 = _extraRewardPerCollateral * 200,000

```solidity
modifier updateReward(address _account) {
        // Disburse available yield from the drip feed
        _disburseTau();

        // If user has collateral, pay down their debt and recycle surplus rewards back into the tauDripFeed.
        uint256 _userCollateral = userDetails[_account].collateral;
        if (_userCollateral > 0) {
            // Get diff between global rewardPerCollateral and user lastUpdatedRewardPerCollateral
            uint256 _rewardDiff = cumulativeTauRewardPerCollateral -
                userDetails[_account].lastUpdatedRewardPerCollateral;

            // Calculate user's TAU earned since the last update, use it to pay off debt
            uint256 _tauEarned = (_rewardDiff * _userCollateral) / Constants.PRECISION;

            if (_tauEarned > 0) {
                uint256 _userDebt = userDetails[_account].debt;
                if (_tauEarned > _userDebt) {
                    // If user has earned more than enough TAU to pay off their debt, pay off debt and add surplus to drip feed
                    userDetails[_account].debt = 0;

                    _withholdTau(_tauEarned - _userDebt);
                    _tauEarned = _userDebt;
                } else {
                    // Pay off as much debt as possible
                    userDetails[_account].debt = _userDebt - _tauEarned;
                }

                emit TauEarned(_account, _tauEarned);
            }
        } else {
            // If this is a new user, add them to the userAddresses array to keep track of them.
            if (userDetails[_account].startTimestamp == 0) {
                userAddresses.push(_account);
                userDetails[_account].startTimestamp = block.timestamp;
            }
        }

        // Update user lastUpdatedRewardPerCollateral
        userDetails[_account].lastUpdatedRewardPerCollateral = cumulativeTauRewardPerCollateral;
        _;
    }

```


## Impact
User will reduce debt.

## Code Snippet
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L78-L118
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/SwapHandler.sol#L90
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/TauDripFeed.sol#L59

## Tool used

Manual Review

## Recommendation
Users need to wait for a period of time before they can withdraw their funds
