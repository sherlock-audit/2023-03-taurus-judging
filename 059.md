KingNFT

medium

# A malicious keeper can steal all yields

## Summary
The security check in ````swap()```` of ````UniswapSwapAdapter```` contract can be bypassed. A malicious keeper can exploit it to steal all yields.

## Vulnerability Detail
The issue arises on L30 of ````swap()```` function, it gets ````swapOutputToken```` by a hard coded offset of ````_swapData````.
```solidity
File: contracts\SwapAdapters\UniswapSwapAdapter.sol
21:     function swap(address _outputToken, bytes calldata _swapData) external returns (uint256) {
22:         // Decode swap data
23:         (uint256 deadline, uint256 _amountIn, uint256 _amountOutMinimum, bytes memory _path) = abi.decode(
24:             _swapData,
25:             (uint256, uint256, uint256, bytes)
26:         );
27: 
28:         // Check that the outputToken is the final token in the path
29:         uint256 length = _swapData.length;
30:         address swapOutputToken = address(bytes20(_swapData[length - 41:length - 21]));
31: 
32:         if (swapOutputToken != _outputToken) { // @audit can be bypassed
33:             // The keeper-inputted Output Token differs from what the contract says it must be.
34:             revert incorrectOutputToken();
35:         }
36: 
37:         // Perform swap (this will fail if tokens haven't been transferred in, or haven't been approved)
38:         ISwapRouter02.ExactInputParams memory params = ISwapRouter02.ExactInputParams({
39:             path: _path,
40:             recipient: msg.sender,
41:             deadline: deadline,
42:             amountIn: _amountIn,
43:             amountOutMinimum: _amountOutMinimum
44:         });
45: 
46:         return ISwapRouter02(swapRouter).exactInput(params);
47:     }
```
But we can append junk data to ````_swapData````, as ````abi.decode()```` doesn't check the extra data.
A simple example to verify it.
```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.19;

contract TestABI {
    function testABI() pure external returns(uint) {
        bytes memory inputWithExtraBytes = abi.encode(1, 2);
        uint onlyFirst = abi.decode(inputWithExtraBytes, (uint));
        return onlyFirst;
    }
}
```

Then, the attacker can bypass the security check on L32 by making ````_swapData```` as
```solidity
abi.encode(deadline, _amountIn, _amountOutMinimum, _pathReal, _pathForged);
```

In the ````_pathReal````, the attacker set the true ````swapOutputToken```` to a valueless token controlled by self.
And create a  uniswap pool of  ````Yield Token```` and ````Attacker Token```` previously.
Add liquidity at a proper price  to make sure the ````exactInput()```` call always returns a very small and negligible result, as the attacker needs to send more than the ````result```` amount of ````Yield Token```` to the vault previously to make sure subsequent processing after the ````swap()```` call  will be successfully executed.

By this way, the ````Yield Token```` is swapped to valueless token, the attacker can withdraw it any time by removing liquidity.
## Impact
A malicious keeper can exploit it to steal all yields.

## Code Snippet
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/SwapAdapters/UniswapSwapAdapter.sol#L21

## Tool used

Manual Review

## Recommendation
```diff
File: contracts\SwapAdapters\UniswapSwapAdapter.sol
21:     function swap(address _outputToken, bytes calldata _swapData) external returns (uint256) {
22:         // Decode swap data
23:         (uint256 deadline, uint256 _amountIn, uint256 _amountOutMinimum, bytes memory _path) = abi.decode(
24:             _swapData,
25:             (uint256, uint256, uint256, bytes)
26:         );
27: 
28:         // Check that the outputToken is the final token in the path
-29:         uint256 length = _swapData.length;
-30:         address swapOutputToken = address(bytes20(_swapData[length - 41:length - 21]));
+            uint256 length = _path.length;
+            address swapOutputToken = address(bytes20(_path[length - 20:length]));
31: 
32:         if (swapOutputToken != _outputToken) { // @audit can be bypassed
33:             // The keeper-inputted Output Token differs from what the contract says it must be.
34:             revert incorrectOutputToken();
35:         }
36: 
37:         // Perform swap (this will fail if tokens haven't been transferred in, or haven't been approved)
38:         ISwapRouter02.ExactInputParams memory params = ISwapRouter02.ExactInputParams({
39:             path: _path,
40:             recipient: msg.sender,
41:             deadline: deadline,
42:             amountIn: _amountIn,
43:             amountOutMinimum: _amountOutMinimum
44:         });
45: 
46:         return ISwapRouter02(swapRouter).exactInput(params);
47:     }
```
