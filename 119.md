RaymondFam

high

# Bad debts inadvertently passed on to liquidators

## Summary
In a situation where an account's debt is worth more than the value of its collateral, liquidators liquidating the account's debt at a discount will end up losing money instead of having the differential bad debt left in the system without any collateral backing it. This loss is absorbed by the liquidators instead of by the system.

## Vulnerability Detail
Here is a practical scenario:

GLP price: 1 USD
TAU price: 1 USD (It is deemed 1 USD per token in the system regardless of the market value)

1. Bob called `modifyPosition()` from GmxYieldAdapter.sol to mint 10_000 tokens of TAU by providing 13_000 tokens of GLP to create an `_accountHealth` equivalent to 1.3e18.
2. On the same day, a market turbulence occurred unexpectedly where GLP price tanked to 0.75 USD.
3. The keeper proceeded to liquidate Bob's account whose `_accountHealth` is now equal to 0.975e18.
4. An arbitrarily large number of `_debtAmount` was inputted to liquidate the entire account, i.e. `_debtAmount == accDetails.debt`.
5. `_calcLiquidation()` was invoked to further invoke `_calcLiquidationDiscount()`.
6.  `diff == MAX_LIQ_DISCOUNT` because [diff = (1.2 + 0.2 - 0.975)1e18 = 0.245e18](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L439-L444) where `liquidationDiscount == 1.2e18`.
7. [`collateralToLiquidate`](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L407) was computed as 10_000 / 0.75 * 1.2e18 = 16_000e18 according to the code lines below, and hence assigned `_accountCollateral`, i.e. 13_000e18.

```solidity
        uint256 collateralToLiquidateWithoutDiscount = (_debtToLiquidate * (10 ** decimals)) / price;
        collateralToLiquidate = (collateralToLiquidateWithoutDiscount * totalLiquidationDiscount) / Constants.PRECISION;
        if (collateralToLiquidate > _accountCollateral) {
            collateralToLiquidate = _accountCollateral;
        }
```
8. Next, [`_getMaxLiquidation`](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L251-L253) worked out to 32_500e18 and had `maxRepay` assigned Bob's debt, i.e. 10_000e18.

```solidity
        maxRepay =
            ((MAX_LIQ_COLL_RATIO * _debt) - ((_collateral * _price * Constants.PRECISION) / (10 ** _decimals))) /
            (MAX_LIQ_COLL_RATIO - _liquidationDiscount);

        // Liquidators cannot repay more than the account's debt
        if (maxRepay > _debt) {
            maxRepay = _debt;
        }
```
9. The following [if block](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L413-L416) was skipped because `_debtToLiquidate == 10_000e18` is not greater than `maxRepay == 10_000e18`.

```solidity
        if (
            _debtToLiquidate >
            _getMaxLiquidation(_accountCollateral, _accountDebt, price, decimals, totalLiquidationDiscount)
        ) revert wrongLiquidationAmount();
```
collateralToLiquidate = 13_000e18
liquidationSurcharge = 10_000 / 0.75 * 0.02 = 266e18

10. Next, the following [if block](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L366-L369) was also skipped because `_minExchangeRate == 0`.

```solidity
        uint256 collateralToLiquidator = collateralToLiquidate - liquidationSurcharge;
        if (collateralToLiquidator < (_debtAmount * _minExchangeRate) / Constants.PRECISION) {
            revert insufficientCollateralLiquidated(_debtAmount, collateralToLiquidator);
        }
```
11. Bob's collateral and debt were set to `0`. Additionally, 10_000 tokens of TAU was burned from the liquidator's account while 12734 and 266 tokens of GLP were respectively transferred to the liquidator and the fee splitter.

```solidity
        userDetails[_account].collateral = accDetails.collateral - collateralToLiquidate;
        userDetails[_account].debt = accDetails.debt - _debtAmount;

        // Burn liquidator's Tau
        TAU(tau).burnFrom(msg.sender, _debtAmount);

        // Transfer part of _debtAmount to liquidator and Taurus as fees for liquidation
        IERC20(collateralToken).safeTransfer(msg.sender, collateralToLiquidator);
        IERC20(collateralToken).safeTransfer(
            Controller(controller).addressMapper(Constants.FEE_SPLITTER),
            liquidationSurcharge
        );
```
## Impact
The liquidator spent $10_000 and got back only (13_000 - 266) * 0.75 = $9550.50 and suffered a loss of $499.50. 

## Code Snippet
[File: LiquidationBot.sol#L100](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/LiquidationBot/LiquidationBot.sol#L100)
[File: BaseVault.sol#L342-L445](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L342-L445)
[File: BaseVault.sol#L240-L261](https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L240-L261)

## Tool used

Manual Review

## Recommendation
Consider adding a check whether or not the liquidation is going to make a profit prior to making TAU burn from `msg.sender` and transferring `collateralToken` to the liquidator and the fee splitter.

```diff
+        (uint256 price, uint8 decimals) = _getCollPrice();
+        uint256 revenue = collateralToLiquidator * price / 10 ** decimals;
       
+        if (revenue < _debtAmount) revert liquidatingAtALoss();

        // Burn liquidator's Tau
        TAU(tau).burnFrom(msg.sender, _debtAmount);

        // Transfer part of _debtAmount to liquidator and Taurus as fees for liquidation
        IERC20(collateralToken).safeTransfer(msg.sender, collateralToLiquidator);
        IERC20(collateralToken).safeTransfer(
            Controller(controller).addressMapper(Constants.FEE_SPLITTER),
            liquidationSurcharge
        );
```
Note: The custom error `liquidatingAtALoss()` will need to be separately added in BaseVault.sol. Ideally, this check should be implemented in the front end so the bot and future liquidators will be supplied with only accounts deemed profitable to liquidate. Where possible, add a reasonable threshold to the right side of the inequality check to absorb gas fee and other miscellaneous expenses, e.g. hired labors etc, just in case the liquidation would entail a break even deal or something close to it.   