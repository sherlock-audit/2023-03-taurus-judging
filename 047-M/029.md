roguereddwarf

medium

# stakedGLP reverts when zero value is transferred

## Summary
The only collateral that is currently supported by the protocol is `stakedGLP`.

`stakedGLP` transfers revert when the value is 0.

This is because the GLP will be unstaked from the sender and staked for the receiver.

This calls downstream the [`RewardTracker._unstake`](https://github.com/gmx-io/gmx-contracts/blob/999757eb189430b034fbbfcd0627f25f24ae3041/contracts/staking/RewardTracker.sol#L252-L270) function.
It contains this line:
```solidity
require(_amount > 0, "RewardTracker: invalid _amount");
```

## Vulnerability Detail
### FeeSplitter.distributeFees function
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Tokenomics/FeeSplitter.sol#L28-L40

`stakedGLP` liquidation fees are sent to the FeeSplitter contract. 
The `proportion` of a feeRecipient may be set to zero or it may be non-zero but the value can be rounded down to zero.
This causes a revert and the function is blocked.

## BaseVault.liquidate function
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/BaseVault.sol#L342-L388
This function may also attempt a zero transfer of `stakedGLP` due to rounding the `liquidationSurcharge` to zero.

## Impact
Zero value transfers of `stakedGLP` which can cause some functionality of the protocol to be blocked.

## Code Snippet
https://github.com/gmx-io/gmx-contracts/blob/999757eb189430b034fbbfcd0627f25f24ae3041/contracts/staking/StakedGlp.sol#L82-L96
```solidity
    function _transfer(address _sender, address _recipient, uint256 _amount) private {
        require(_sender != address(0), "StakedGlp: transfer from the zero address");
        require(_recipient != address(0), "StakedGlp: transfer to the zero address");


        require(
            glpManager.lastAddedAt(_sender).add(glpManager.cooldownDuration()) <= block.timestamp,
            "StakedGlp: cooldown duration not yet passed"
        );


        IRewardTracker(stakedGlpTracker).unstakeForAccount(_sender, feeGlpTracker, _amount, _sender);
        IRewardTracker(feeGlpTracker).unstakeForAccount(_sender, glp, _amount, _sender);


        IRewardTracker(feeGlpTracker).stakeForAccount(_sender, _recipient, glp, _amount);
        IRewardTracker(stakedGlpTracker).stakeForAccount(_recipient, _recipient, feeGlpTracker, _amount);
    }
```

https://github.com/gmx-io/gmx-contracts/blob/999757eb189430b034fbbfcd0627f25f24ae3041/contracts/staking/RewardTracker.sol#L252-L270
```solidity
    function _unstake(address _account, address _depositToken, uint256 _amount, address _receiver) private {
        require(_amount > 0, "RewardTracker: invalid _amount");
        require(isDepositToken[_depositToken], "RewardTracker: invalid _depositToken");


        _updateRewards(_account);


        uint256 stakedAmount = stakedAmounts[_account];
        require(stakedAmounts[_account] >= _amount, "RewardTracker: _amount exceeds stakedAmount");


        stakedAmounts[_account] = stakedAmount.sub(_amount);


        uint256 depositBalance = depositBalances[_account][_depositToken];
        require(depositBalance >= _amount, "RewardTracker: _amount exceeds depositBalance");
        depositBalances[_account][_depositToken] = depositBalance.sub(_amount);
        totalDepositSupply[_depositToken] = totalDepositSupply[_depositToken].sub(_amount);


        _burn(_account, _amount);
        IERC20(_depositToken).safeTransfer(_receiver, _amount);
    }
```


## Tool used
Manual Review

## Recommendation
Only execute the transfer if the amount is greater zero.